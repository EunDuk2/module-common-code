plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'  // module-common의 groupId
version = '1.0.9'      // 버전 (수정 및 배포시 무조건 다르게 설정 필요)
description = 'module-common'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// jar 생성 활성화
jar {
    enabled = true
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.10"
    }
}

// 추가한 공통 코드의 의존성이 필요한 경우(ex. redis) compileonly의존성을 추가
dependencies {
    // 컴파일 시점에만 필요 (런타임은 소비자가 제공)
    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'io.lettuce:lettuce-core'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'

    // Hibernate (BaseTimeEntity용)
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    compileOnly 'org.hibernate.orm:hibernate-core:6.5.2.Final'  // Hibernate @CreationTimestamp, @UpdateTimestamp

    // ✅ AWS SDK (S3Uploader / AwsS3Config 용)
    compileOnly platform("software.amazon.awssdk:bom:2.29.33")
    compileOnly 'software.amazon.awssdk:s3'
    compileOnly 'software.amazon.awssdk:auth'
    compileOnly 'software.amazon.awssdk:regions'

    // ✅ MultipartFile (S3Uploader에서 사용)
    compileOnly 'org.springframework:spring-web'
}


// GitHub Packages 배포 설정 (비밀정보는 Github Secret에서 제공)
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            // YOUR_GITHUB_USERNAME을 실제 GitHub 사용자 이름으로 변경하세요.
            url = uri("https://maven.pkg.github.com/Eunduk2/module-common")

            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.properties["gpr.user"]
                password = System.getenv("GITHUB_TOKEN") ?: project.properties["gpr.key"]
            }
        }
    }
}